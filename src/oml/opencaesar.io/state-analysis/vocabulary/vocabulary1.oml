@dc:title "Example Vocabulary"
@dc:creator "Example Company"
@dc:rights "Copyright 2019, by Example Company"
vocabulary <http://opencaesar.io/state-analysis/vocabulary/vocabulary1#> as state-analysis {
	
	extends <http://www.w3.org/2000/01/rdf-schema#> as rdfs
	extends <http://purl.org/dc/elements/1.1/> as dc
	
	extends <http://imce.jpl.nasa.gov/foundation/base#> as base
	extends <http://imce.jpl.nasa.gov/foundation/analysis#> as analysis

	//
	// S E S S I O N  1
	//
	
	// Slide 15
		
	@rdfs:label "Consumed"
	@dc:description "Consumed is the class of all things consumed."
	aspect Consumed
	
	@rdfs:label "Consumer"
	@dc:description "Consumer is the class of all consumers."
	aspect Consumer
	
	@rdfs:label "Provided"
	@dc:description "Provided is the class of all things provided."
	aspect Provided
	
	@rdfs:label "Provider"
	@dc:description "Provider is the class of all providers."
	aspect Provider
	
	@rdfs:label "Command"
	concept Command < base:IdentifiedElement, Consumed, Provided
	
	@rdfs:label "Measurement"
	concept Measurement < base:IdentifiedElement, Consumed, Provided
	
	@rdfs:label "Control System"
	@dc:description "The functionality of control is separate from the rest of the system."
	concept ControlSystem < base:IdentifiedElement, Consumer, Provider [
		restricts all consumes to Measurement
		restricts all provides to Command
	]
	
	@rdfs:label "System Under Control"
	@dc:description "The vehicle and its environment are considered together as an integrated entity."
	concept SystemUnderControl < base:IdentifiedElement, Consumer, Provider [
		restricts all consumes to Command
		restricts all provides to Measurement
	]
	
	@rdfs:label "Consumes"
	@dc:description "Consumes is the relation from a Consumer to a Consumed."
	relation entity Consumes [
		from Consumer
		to Consumed
		forward consumes
	]
	
	@rdfs:label "Provides"
	@dc:description "Provides is the relation from a Provider to the Provided."
	relation entity Provides [
		from Provider
		to Provided
		forward provides
		inverse functional
	]
	
	//
	// S E S S I O N  2
	//
	
	// Slide 13
	
	@rdfs:label "State Variable"
	@dc:description "A state is a property of a thing."
	concept StateVariable < base:IdentifiedElement
	
	// Slide 14
	
	@rdfs:label "Affected"
	@dc:description "Affected is the class of all things affected by state effects."
	concept Affected
	
	@rdfs:label "Affector"
	@dc:description "Affector is the class of all things that affect state variables."
	concept Affector < analysis:AnalyzedElement
	
	ref concept StateVariable < Affected, Affector
	
	@rdfs:label "Affects"
	@dc:description "Affects is the relation from an Affector to an Affected."
	relation entity Affects [
		from Affector
		to Affected
		forward affects
		reverse isAffectedBy
		asymmetric
		irreflexive
	]
	
	// Slide 16
	
	ref concept Measurement < Affected
	
	relation entity IsMeasurandOf [
		from StateVariable
		to Measurement
		forward isMeasurandOf
		reverse hasMeasurand
		functional
		inverse functional
		asymmetric
		irreflexive
	] < Affects
	
	// Slide 18

	ref concept Command < Affector [
		restricts affects to max 1 StateVariable
	]
	
	relation entity Commands [
		from Command
		to StateVariable
		forward commands
		reverse isCommandedBy
		functional
		inverse functional
		asymmetric
		irreflexive
	] < Affects
	
	// Slide 26
	
	ref concept StateVariable < analysis:CharacterizedElement
	
	concept Model < analysis:Explanation
	
	concept StateEffectsModel < Model [
		restricts all analysis:analyzes to StateVariable
		restricts all analysis:explains to StateVariable
		restricts analysis:explains to max 1 StateVariable
	]
	
	// Slide 27
	
	ref concept Measurement < analysis:CharacterizedElement, Consumed, Provided
	
	concept MeasurementModel < Model, EstimatorInput [
		restricts all analysis:analyzes to StateVariable
		restricts all analysis:explains to Measurement
		restricts analysis:explains to max 1 Measurement	
	]
	
	// Slide 32
	
	concept CommandModel < Model, EstimatorInput [
		restricts all analysis:analyzes to Affector
		restricts all analysis:explains to StateVariable
		restricts analysis:explains to max 1 StateVariable		
	]
	
	// Slide 49
	
	concept StateFunction < analysis:Characterization [
		restricts all analysis:characterizes to StateVariable
	]
	
	// Slide 56
	
	aspect EstimatorInput < analysis:AnalyzedElement
	
	ref concept Measurement < EstimatorInput
	ref concept Command < EstimatorInput
	ref concept Estimate < EstimatorInput
	
	concept Executor < analysis:Explanation
	
	concept Estimate < base:IdentifiedElement, analysis:CharacterizedElement, EstimatorInput, Executor

	concept Estimator < Executor [
		restricts all analysis:analyzes to EstimatorInput
		restricts all analysis:explains to Estimate
	]
	
	concept KnowledgeGoal < EstimatorInput, Goal
	
	relation entity IsEstimateOf [
		from Estimate
		to StateVariable
		forward isEstimateOf
		reverse hasEstimate
		functional
		inverse functional
		asymmetric
		irreflexive
	]
	
	relation entity AnalyzesMeasurementForEstimation [
		from Estimator
		to Measurement
		forward analyzesMeasurementForEstimation
	] < analysis:Analyzes

	relation entity AnalyzesCommandForEstimation [
		from Estimator
		to Command
		forward analylzesCommandForEstimation
	] < analysis:Analyzes
	
	relation entity AnalyzesEstimateForEstimation [
		from Estimator
		to Estimate
		forward analyzesEstimateForEstimation
	] < analysis:Analyzes
	
	relation entity AnalyzesMeasurementModelForEstimation [
		from Estimator
		to MeasurementModel
		forward analyzesMeasurementModelForEstimation
	] < analysis:Analyzes

	relation entity AnalyzesCommandModelForEstimation [
		from Estimator
		to CommandModel
		forward analyzesCommandModelForEstimation
	] < analysis:Analyzes
	
	relation entity AnalyzesKnowledgeGoalForEstimation [
		from Estimator
		to KnowledgeGoal
		forward analyzesKnowledgeGoalForEstimation
	] < analysis:Analyzes

	// Slide 65
	
	ref concept Command < analysis:CharacterizedElement, Consumed, Provided
	
	aspect ControllerInput < analysis:AnalyzedElement
	
	concept Controller < Executor [
		restricts all analysis:analyzes to ControllerInput
		restricts all analysis:explains to Command
	]
	
	concept ControlGoal < ControllerInput, Goal
	
	relation entity AnalyzesEstimateForControl [
		from Controller
		to Estimate
		forward analyzesEstimateForControl
	]
	
	relation entity AnalyzesMeasurementModelForControl [
		from Controller
		to MeasurementModel
		forward analyzesMeasurementModelForControl
	] < analysis:Analyzes

	relation entity AnalyzesCommandModelForControl [
		from Controller
		to CommandModel
		forward analyzesCommandModelForControl
	] < analysis:Analyzes

	relation entity AnalyzesStateEffectsModelForControl [
		from Controller
		to StateEffectsModel
		forward analyzesStateEffectsModelForControl
	] < analysis:Analyzes

	relation entity AnalyzesKnowledgeGoalForControl [
		from Estimator
		to KnowledgeGoal
		forward analyzesKnowledgeGoalForControl
	] < analysis:Analyzes

	// Slide 81
	
	concept Goal < base:IdentifiedElement
	
	concept TimePoint < base:IdentifiedElement
	
	concept TimeInterval < base:IdentifiedElement
	
	concept Constraint < base:IdentifiedElement
	
	relation entity BeginsAt [
		from TimeInterval
		to TimePoint
		forward beginsAt
		functional
	]
	
	relation entity EndsAt [
		from TimeInterval
		to TimePoint
		forward endsAt
		functional
	]
	
	relation entity AssertsConstraint [
		from Goal
		to Constraint
		forward assertsConstraint
	]
	
	relation entity AppliesDuring [
		from Goal
		to TimeInterval
		forward appliesDuring
	]
	
	relation entity Constrains [
		from Goal
		to StateVariable
		forward constrains
		functional
	]
	
	// Slide 92
	
	concept MaintenanceGoal < ControlGoal
	
	// Slide 93
	
	concept TransitionGoal < ControlGoal
	
	
	//
	// S E S S I O N  3 A
	//
	
	// Slide 10
	
	concept Elaboration < analysis:Explanation
	
	relation entity ElaboratesFrom [
		from Goal
		to Goal
		forward elaboratesFrom
	]
	
	relation entity ElaboratesTo [
		from Goal
		to Goal
		forward elaboratesTo
	]

}
